plugins {
	id 'java'
	id 'com.google.protobuf' version '0.9.4'
}

def grpcVersion = '1.63.0'
def protobufVersion = '3.25.3'

def ghidraInstallDir

if (System.env.GHIDRA_INSTALL_DIR) {
	ghidraInstallDir = System.env.GHIDRA_INSTALL_DIR
}
else if (project.hasProperty("GHIDRA_INSTALL_DIR")) {
	ghidraInstallDir = project.getProperty("GHIDRA_INSTALL_DIR")
}
else {
	ghidraInstallDir = "C:/Users/djsteffey/Desktop/ghidra/ghidra_11.3.1_PUBLIC"
}

task distributeExtension {
	group "Ghidra"

	apply from: new File(ghidraInstallDir).getCanonicalPath() + "/support/buildExtension.gradle"
	dependsOn ':buildExtension'
}

extractIncludeProto.mustRunAfter(copyDependencies)
extractIncludeTestProto.mustRunAfter(copyDependencies)

repositories {
	mavenCentral()
}

dependencies {
	implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
	implementation "io.grpc:grpc-protobuf:$grpcVersion"
	implementation "io.grpc:grpc-stub:$grpcVersion"

	// Optional: for reflection-based service discovery
	implementation "io.grpc:grpc-services:$grpcVersion"

	// Protobuf compiler
	implementation "com.google.protobuf:protobuf-java:$protobufVersion"
	implementation "com.google.protobuf:protobuf-kotlin:$protobufVersion"

	// Annotation processor for gRPC
	compileOnly "org.apache.tomcat:annotations-api:6.0.53"
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:$protobufVersion"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {}
			}
		}
	}
}

sourceSets{
	main{
		proto{
            srcDir("../proto")
		}
	}
}
